@create Game of Uno=10
@lock Game of Uno=#87
&ZDESC Game of Uno=[repeat(-,78)]%r%r[space(2)]Type '[ansi(hc,uno help)]' for instructions.%r%r[space(2)]To play, type '[ansi(hc,join uno)]'.%r%r[space(2)]To watch the game in progress, type '[ansi(hc,watch uno)]'.%r%r[space(2)]To make the computer play, type '[ansi(hc,player join)]'.%r%r[space(2)]To make the computer take its turn, if it joined first, type '[ansi(hc,player start)]'.%r%r[space(2)]The top card is a [u(card,first(v(stack)))].%r[space(2)][switch(v(players),,The game has not started.,switch(v(turn),,The game is over.,edit(u(elist,edit(iter(v(players),[switch(##,player,Computer_Player,moniker(##))]\([words(v(hand_##))]\)) ,%b,|),and,|),_,%b) [switch(words(v(players)),1,is,are)] playing the game.%r%rIt is [moniker(v(turn))]'s turn.))]%r%r[repeat(-,78)]
&ELIST Game of Uno=squish(ifelse(gt(words(%0,%2),1),iter(extract(%0,1,sub(words(%0,%2),2),%2),##\\\,%b,%2)[extract(%0,dec(words(%0,%2)),1,%2)] %1 [last(%0,%2)],%0))
@VA Game of Uno=$uno help:@pemit %#=repeat(-,78)%r[u(help)]%r[repeat(-,78)]
&HELP Game of Uno=%r%tThe goal of Uno is to get rid of all your cards. In each turn, you must play a card that is either the same color or same name/number as the last card played. If you don't have any card that can be played, you must draw until you do.%r%r%tThe special cards are "Draw Two" (the next player draws two cards and loses his turn), "Skip" (the next player loses his turn), "Reverse" (the direction of play reverses), "Wild" (can be played any time - changes the color), and "Wild Draw Four" (can only be played if you have no cards of the right color - changes the color, next player draws four cards and loses his turn).%r%r[ansi(h,Commands:)]%rURESET - Reset the game.%rJOIN UNO - Join a game in progress.%rWATCH UNO - Watch a game in progress.%rUNWATCH UNO - Stop watching.%rUINV - Look at your hand.%rUPLAY <card> - Play a card (see 'uno list').%rUWILD <color> - Play a wild card.%rUWDRAW <color> - Play a wild draw four.%rUDRAW - Draw a card.%rUSHUFFLE - shuffle the pile back into the deck.%rUPOINTS - See how many points are in your hand (see 'uno scoring').%rUSTATS - List players with the best average scores in at least 2 games.
&FULLDECK Game of Uno=r0 g0 b0 y0 r1 r1 g1 g1 b1 b1 y1 y1 r2 r2 g2 g2 b2 b2 y2 y2 r3 r3 g3 g3 b3 b3 y3 y3 r4 r4 g4 g4 b4 b4 y4 y4 r5 r5 g5 g5 b5 b5 y5 y5 r6 r6 g6 g6 b6 b6 y6 y6 r7 r7 g7 g7 b7 b7 y7 y7 r8 r8 g8 g8 b8 b8 y8 y8 r9 r9 g9 g9 b9 b9 y9 y9 rdraw rdraw gdraw gdraw bdraw bdraw ydraw ydraw rskip rskip gskip gskip bskip bskip yskip yskip rrev rrev grev grev brev brev yrev yrev wild wild wild wild wdraw wdraw wdraw wdraw
&URESET Game of Uno=$ureset:@wipe me/hand_*;&players me;&prize me;&deck me=shuffle(v(fulldeck));&stack me=first(v(deck));&deck me=rest(v(deck));@unlock/use %vc;&turn me;&watchers me;@emit %K resets the game of Uno. The top card is now: [u(card,v(stack))].
&HAND Game of Uno=u(elist,iter(sort(v(hand_%0)),u(card,##)[ifelse(eq(#@,words(v(hand_%0))),,|)]),and,|)
&UINV Game of Uno=$uinv:@swi member(v(players),%#)=0,{@pemit %#=You're not in the game.},{@pemit %#=Your cards: [u(hand,%#)].}
&STACK Game of Uno=g9
&CARD Game of Uno=switch(%0,wild,Wild,wdraw,Wild Draw Four,switch(mid(%0,0,1),r,%xh%xrRed,g,%xh%xgGreen,b,%xh%xbBlue,y,%xh%xyYellow) [switch(after(%0,mid(%0,0,1)),draw,Draw Two,skip,Skip,rev,Reverse,wild,%(Wild%),wdraw,%(Wild Draw Four%),after(%0,mid(%0,0,1)))]%xn)
&UDRAW Game of Uno=$udraw:@sel 0=member(v(players),%#),{@pemit %#=You're not in the game.},strmatch(v(turn),%#),{@pemit %#=It's not your turn.},{@pemit/list [setdiff(u(watchers),%#)]=%K draws a card.;@pemit %#=You draw a [u(card,first(v(deck)))].;&hand_%# me=cat(v(hand_%#),first(v(deck)));&deck me=rest(v(deck))};@switch [words(v(hand_%#))]=1,{@emit %K grumbles, "Not uno anymore..."};@switch [words(v(deck))]=0,{@tr me/do_shuffle=%#}}
@VB Game of Uno=$uno list:@pemit %#=repeat(-,78)%r[u(list)]%r[repeat(-,78)]
&LIST Game of Uno=The card name consists of a color letter (r, g, b, y) followed by a specifier.%rThe specifier is either a number from 0 to 9, "skip", "rev", or "draw".%r%rExample:%r g0 = Green 0%r rrev = Red Reverse%r ydraw = Yellow Draw Two%r wild = Wild%r wdraw = Wild Draw Four
&UPLAY Game of Uno=$uplay *:@sel 0=member(v(players),%#),{@pemit %#=You're not in the game.},strmatch(v(turn),%#),{@pemit %#=It's not your turn.},member(lcstr(v(hand_%#)),lcstr(%0)),{@pemit %#=You don't have that card. Take a look at 'uinv' and 'uno list'.},not(strmatch(%0,w*)),{@pemit %#=Wild cards must be played with 'uwild <color>' or 'uwdraw <color>'.},corbool(strmatch(mid(%0,0,1),mid(first(v(stack)),0,1)),strmatch(mid(%0,1,4),mid(first(v(stack)),1,4))),{@pemit %#=That card doesn't match the color or name/number of the top card.},not(eq(words(v(hand_%#)),1)),{@pemit/list u(watchers)=%K plays a [u(card,lcstr(%0))].%r%xr***%xn %K has won at Uno! %xr***%xn;&turn me;give %K=[v(prize)];&hand_%# me;@pemit/list v(watchers)=Scores:[iter(v(players),%r%b%b[ljust(switch(##,player,Player,moniker(##)),15)][rjust(u(calc_score,v(hand_##)),10)])];@dolist v(players)={&record_## me=[setq(0,v(record_##))][fdiv(setr(1,add(extract(%q0,2,1,/),u(calc_score,v(hand_##)))),setr(2,add(1,last(%q0,/))))]/%q1/%q2};ureset},not(strmatch(%0,*rev)),{&stack me=lcstr(%0) [v(stack)];&hand_%# me=remove(lcstr(v(hand_%#)),lcstr(%0));@pemit/list u(watchers)=%K plays a [u(card,first(v(stack)))].;&players me=revwords(v(players));@pemit %#=Your hand is now: [u(hand,%#)].;&turn me=first(v(players));@pemit/list u(watchers)=It is now [if(strmatch(v(turn),player),player,moniker(v(turn)))]'s turn.;@switch [words(v(hand_%#))]=1,{@emit %K screams, "UNO!"};@switch v(turn)=player,{@tr me/playergo}},not(strmatch(%0,*skip)),{&stack me=lcstr(%0) [v(stack)];&hand_%# me=remove(lcstr(v(hand_%#)),lcstr(%0));@pemit/list u(watchers)=%K plays a [u(card,first(v(stack)))].;&players me=rest(v(players)) [first(v(players))];&players me=rest(v(players)) [first(v(players))];@pemit %#=Your hand is now: [u(hand,%#)].;&turn me=first(v(players));@pemit/list u(watchers)=It is now [switch(v(turn),player,Player,[moniker(v(turn))])]'s turn.;@switch [words(v(hand_%#))]=1,{@emit %K screams, "UNO!"};@switch v(turn)=player,{@tr me/playergo}},not(strmatch(%0,*draw)),{&stack me=lcstr(%0) [v(stack)];&hand_%# me=remove(lcstr(v(hand_%#)),lcstr(%0));@pemit/list u(watchers)=%K plays a [u(card,first(v(stack)))].;&players me=rest(v(players)) [first(v(players))];@pemit [first(v(players))]=You draw a [u(card,first(v(deck)))] and a [u(card,extract(v(deck),2,1))].;&hand_[first(v(players))] me=cat(v(hand_[first(v(players))]),extract(v(deck),1,2));&deck me=extract(v(deck),3,sub(words(v(deck)),2));&players me=rest(v(players)) [first(v(players))];@pemit %#=Your hand is now: [u(hand,%#)].;&turn me=first(v(players));@pemit/list u(watchers)=It is now [switch(v(turn),player,Player,[moniker(v(turn))])]'s turn.;@switch [words(v(hand_%#))]=1,{@emit %K screams, "UNO!"};@switch v(turn)=player,{@tr me/playergo}},{&stack me=lcstr(%0) [v(stack)];&hand_%# me=remove(lcstr(v(hand_%#)),lcstr(%0));@pemit/list u(watchers)=%K plays a [u(card,first(v(stack)))].;&players me=rest(v(players)) [first(v(players))];@pemit %#=Your hand is now: [u(hand,%#)].;&turn me=first(v(players));@pemit/list u(watchers)=It is now [switch(v(turn),player,Player,[moniker(v(turn))])]'s turn.;@switch [words(v(hand_%#))]=1,{@emit %K screams, "UNO!"};@switch v(turn)=player,{@tr me/playergo}}
&UWILD Game of Uno=$uwild *:@sel 0=member(v(players),%#),{@pemit %#=You're not in the game.},strmatch(v(turn),%#),{@pemit %#=It's not your turn.},member(lcstr(v(hand_%#)),wild),{@pemit %#=You don't have a Wild.},member(r g b y,lcstr(mid(%0,0,1))),{@pemit %#=What kind of color is "%0"?},not(eq(words(v(hand_%#)),1)),{@pemit/list u(watchers)=%K plays a wild, but it doesn't matter because%r%xr***%xn %K has won at Uno! %xr***%xn;&hand_%# me;@pemit/list v(watchers)=Scores:[iter(v(players),%r%b%b[ljust(switch(##,player,Player,moniker(##)),15)][rjust(u(calc_score,v(hand_##)),10)])];@dolist v(players)={&record_## me=[setq(0,v(record_##))][fdiv(setr(1,add(extract(%q0,2,1,/),u(calc_score,v(hand_##)))),setr(2,add(1,last(%q0,/))))]/%q1/%q2};ureset;&turn me;give %K=[v(prize)]},{&hand_%# me=remove(v(hand_%#),wild);&stack me=lcstr(mid(%0,0,1))wild [v(stack)];@pemit/list u(watchers)= %K plays a Wild and changes the color to %xh[switch(%0,r*,%xrred,g*,%xggreen,b*,%xbblue,%xyyellow)]%xn.;&players me=rest(v(players)) [first(v(players))];@pemit %#=Your hand is now: [u(hand,%#)].;&turn me=first(v(players));@pemit/list u(watchers)=It is now [switch(v(turn),player,Player,[moniker(v(turn))])]'s turn.;@switch [words(v(hand_%#))]=1,{@emit %K screams, "UNO!"};@switch v(turn)=player,{@tr me/playergo}}
&NOMATCH Game of Uno=not(words(iter(v(hand_%0),switch(corbool(strmatch(##,wild),strmatch(mid(##,0,1),mid(first(v(stack)),0,1)),candbool(strmatch(after(##,mid(##,0,1)),after(first(v(stack)),mid(first(v(stack)),not(strmatch(##,wdraw))),0,1)))),1,x))))
&UWDRAW Game of Uno=$uwdraw *:@sel 0=member(v(players),%#),{@pemit %#=You're not in the game.},strmatch(v(turn),%#),{@pemit %#=It's not your turn.},member(lcstr(v(hand_%#)),wdraw),{@pemit %#=You don't have a Wild Draw Four.},not(match(v(hand_%#),strtrunc(first(v(stack)),1)*)),{@pemit %#=You have another card that you can play.},member(r g b y,lcstr(mid(%0,0,1))),{@pemit %#=What kind of color is "%0"?},not(eq(words(v(hand_%#)),1)),{@pemit/list u(watchers)=%K plays a Wild Draw Four, but it doesn't matter because%r%xr***%xn %K has won at Uno! %xr***%xn;&hand_%# me;@pemit/list v(watchers)=Scores:[iter(v(players),%r%b%b[ljust(switch(##,player,Player,moniker(##)),15)][rjust(u(calc_score,v(hand_##)),10)])];@dolist v(players)={&record_## me=[setq(0,v(record_##))][fdiv(setr(1,add(extract(%q0,2,1,/),u(calc_score,v(hand_##)))),setr(2,add(1,last(%q0,/))))]/%q1/%q2};ureset;&turn me;give %K=[v(prize)]},{&hand_%# me=remove(v(hand_%#),wdraw);&stack me=lcstr(mid(%0,0,1))wdraw [v(stack)];@pemit/list u(watchers)=%K plays a Wild Draw Four and changes the color to %xh[switch(%0,r*,%xrred,g*,%xggreen,b*,%xbblue,%xyyellow)]%xn.;&players me=rest(v(players)) [first(v(players))];@pemit [first(v(players))]=You draw a [u(card,first(v(deck)))], a [u(card,extract(v(deck),2,1))], a [u(card,extract(v(deck),3,1))], and a [u(card,extract(v(deck),4,1))].;&hand_[first(v(players))] me=cat(v(hand_[first(v(players))]),extract(v(deck),1,4));&deck me=extract(v(deck),5,sub(words(v(deck)),4));&players me=rest(v(players)) [first(v(players))];@pemit %#=Your hand is now: [u(hand,%#)].;&turn me=first(v(players));@pemit/list u(watchers)=It is now [switch(v(turn),player,Player,[moniker(v(turn))])]'s turn.;@switch [words(v(hand_%#))]=1,{@emit %K screams, "UNO!"};@switch v(turn)=player,{@tr me/playergo}}
&HDESC Game of Uno=The amazing game of Uno is here.
@Desc Game of Uno=[u(zdesc)]
&START Game of Uno=$join uno:@swi member(v(players),%#)=0,{@emit %K joins the game of uno.;@swi hasattr(me,turn)=0,{&turn me=%#};&hand_%# me=extract(v(deck),1,7);&players me=cat(v(players),%#);&deck me=extract(v(deck),8,sub(words(v(deck)),7));@pemit %#=Your hand: [u(hand,%#)].;&watchers me=%# [u(watchers)]},{@pemit %#=You are already in the game!}
&WATCH Game of Uno=$watch uno:@pemit %#=You watch the game of uno.;@oemit %#=%K watches the game of uno.;&watchers me=setunion(v(watchers),%#)
@VI Game of Uno=(
@VO Game of Uno=)
@VC Game of Uno=#13
&PLAYERGO Game of Uno=@switch/all [u(cango,num(me))][words(v(hand_player))] [u(playercard,gt(words(setr(0,filter(prefix_filter,u(hand_player)))),0),gt(words(setr(1,filter(suffix_filter,u(hand_player)))),0))]=0* *,{&hand_player me=v(hand_player) [first(v(deck))];&deck me=rest(v(deck));@pemit/list v(watchers)=ifelse(strmatch(words(v(hand_player)),2),%vn grumbles\, "Not uno anymore..."%r,)%vn draws a card.;@tr me/playergo},11 *,{@pemit/list u(watchers)=%xr***%xn %vn wins the uno game!%xr***%xn;&hand_player me;@pemit/list v(watchers)=Scores:[iter(v(players),%r%b%b[ljust(switch(##,player,Player,moniker(##)),15)][rjust(u(calc_score,v(hand_##)),10)])];@dolist v(players)={&record_## me=[setq(0,v(record_##))][fdiv(setr(1,add(extract(%q0,2,1,/),u(calc_score,v(hand_##)))),setr(2,add(1,last(%q0,/))))]/%q1/%q2};ureset},12 *,{@pemit/list u(watchers)=%vn screams, "UNO!"},1* *rev,{@pemit/list v(watchers)=%vn plays a [u(card,%q7)].;&players me=first(revwords(v(players))) [rest(revwords(v(players)))];&turn me=first(v(players));@pemit/list v(watchers)=It is now [switch(v(turn),player,Player,[moniker(v(turn))])]'s turn.;&hand_player me=[remove(v(hand_player),first(%q7))];&stack me=%q7 [v(stack)];@switch v(turn)=player,{@tr me/playergo}},1* *skip,{@pemit/list v(watchers)=%vn plays a [u(card,%q7)];&stack me=%q7 [v(stack)];&players me=rest(v(players)) [first(v(players))];&players me=rest(v(players)) [first(v(players))];&turn me=first(v(players));@pemit/list v(watchers)=It is now [switch(v(turn),player,Player,[moniker(v(turn))])]'s turn.;&hand_player me=[remove(v(hand_player),first(%q7))];@switch v(turn)=player,{@tr me/playergo}},1* ?draw,{@pemit/list v(watchers)=%vn plays a [u(card,%q7)];&stack me=%q7 [v(stack)];&players me=rest(v(players)) [first(v(players))];@pemit first(v(players))=You draw a [u(card,first(v(deck)))] and a [u(card,first(rest(v(deck))))].;&hand_[first(v(players))] me=extract(v(deck),1,2) [v(hand_[first(v(players))])];&deck me=rest(rest(v(deck)));&players me=rest(v(players)) [first(v(players))];&turn me=first(v(players));@pemit/list v(watchers)=It is now [switch(v(turn),player,Player,[moniker(v(turn))])]'s turn.;&hand_player me=[remove(v(hand_player),first(%q7))];@switch v(turn)=player,{@tr me/playergo}},1* wild ?,{&players me=rest(v(players)) [first(v(players))];&stack me=%q5wild [v(stack)];@pemit/list v(watchers)=%vn plays a wild and changes the color to [ansi(h%q5,grab(red blue green yellow,%q5*))][setq(7,first(%q7))].;&turn me=first(v(players));@pemit/list v(watchers)=It is now [switch(v(turn),player,Player,[moniker(v(turn))])]'s turn.;&hand_player me=[remove(v(hand_player),first(%q7))];@switch v(turn)=player,{@tr me/playergo}},1* wdraw ?,{&players me=rest(v(players)) [first(v(players))];&stack me=%q5wdraw [v(stack)];@pemit/list v(watchers)=%vn plays a wild draw four and changes the color to [ansi(h%q5,grab(red blue green yellow,%q5*))][setq(7,first(%q7))].;@pemit first(v(players))=You draw a [u(card,setr(3,first(v(deck))))], [u(card,setr(4,first(rest(v(deck)))))], [u(card,setr(8,extract(v(deck),3,1)))] and a [u(card,setr(9,extract(v(deck),4,1)))].;&hand_[first(v(players))] me=extract(v(deck),1,4) [v(hand_[first(v(players))])];&Deck me=after(v(Deck),extract(v(deck),1,4));&players me=rest(v(players)) [first(v(players))];&turn me=first(v(players));@pemit/list v(watchers)=It is now [switch(v(turn),player,Player,[moniker(v(turn))])]'s turn.;&hand_player me=[remove(v(hand_player),first(%q7))];@switch v(turn)=player,{@tr me/playergo}},1* ??,{@pemit/list v(watchers)=%vn plays a [u(card,%q7)].;&players me=rest(v(players)) [first(v(players))];&stack me=%q7 [v(stack)];&turn me=first(v(players));@pemit/list v(watchers)=It is now [switch(v(turn),player,Player,[moniker(v(turn))])]'s turn.;&hand_player me=[remove(v(hand_player),first(%q7))];@switch v(turn)=player,{@tr me/playergo}}
&PLAYERJOIN Game of Uno=$player join:&players me=v(players) player;@emit %vn joins the uno game.;&hand_player uno=extract(v(deck),1,7);&deck uno=after(v(deck),extract(v(deck),1,7))
&PLAYERSTART Game of Uno=$player start:@tr me/playergo
&PLAYERCARD Game of Uno=setr(7,switch(%0%1,1?,first(shuffle(%q0)),?1,first(shuffle(%q1)),[first(shuffle(filter(wild_filter,v(hand_player))))] [setr(5,first(shuffle(iter(v(hand_player),ifelse(strmatch(##,w*),,mid(##,0,1))))))]))
&WILD_FILTER Game of Uno=strmatch(%0,w*)
&CANGO Game of Uno=gt(words(edit(iter(v(hand_player),corbool(strmatch(##,wild),strmatch(##,wdraw),strmatch(mid(##,0,1),mid(first(v(stack)),0,1)),candbool(strmatch(delete(##,0,1),delete(first(v(stack)),0,1)),not(strmatch(##,wdraw))))),0,)),0)
@VN Game of Uno=Player
&PREFIX_FILTER Game of Uno=strmatch(mid(%0,0,1),mid(first(v(stack)),0,1))
&SUFFIX_FILTER Game of Uno=candbool(strmatch(delete(%0,0,1),delete(first(v(stack)),0,1)),not(strmatch(%0,w*)))
&SHUFFLE Game of Uno=$shuffle:@tr me/do_shuffle=%#
&CALC_SCORE Game of Uno=[setq(9,0)][parse(%0,setq(9,add(%q9,switch(##,w*,50,?r*,20,?s*,20,?d*,20,delete(##,0,1)))))]%q9
&USTATS Game of Uno=$ustats:@pemit %#=[space(4)][ljust(Top Uno Players,31)]Games[space(4)]Points[space(3)]Average%r[repeat(-,60)][parse(extract(sortby(comp_records,filter(filter_2games,parse(lattr(me/record_#*),moniker(after(##,RECORD_))/[v(##)]|)Player/[v(record_player)],|),|),1,10,|),%r[rjust(#@,2)].%b[ljust(first(##,/),25)][rjust(last(##,/),10)][rjust(extract(##,3,1,/),10)][rjust(round(extract(##,2,1,/),2),10)],|)]%r[repeat(-,60)]
&UPOINTS Game of Uno=$upoints:@pemit %#=switch(hasattr(me,hand_%#),0,You're not playing uno.,Your hand contains [u(calc_score,v(hand_%#))] points.)
&UNWATCH Game of Uno=$unwatch uno:@switch 0=member(v(watchers),%#),@pemit %#=You're not in the game.,member(v(players),%#),{@pemit %#=You stop watching the game of uno.;@oemit %#=%K stops watching the game of uno.;&watchers me=remove(v(watchers),%#)},@pemit %#=Don't quit watching while you're playing. Use uleave or ureset to quit.
&FILTER_2GAMES Game of Uno=gt(last(%0,/),1)
&COMP_RECORDS Game of Uno=sign(sub(extract(%0,2,1,/),extract(%1,2,1,/)))
&SCORING Game of Uno=$uno scoring:@pemit %#=[repeat(-,78)]%rPoints are bad in this version of Uno. At the end of the game, each card in your hand is counted against you as follows:%r%t* Number cards are counted at face value.%r%t* Draw Two, Skip, and Reverse cards are counted as 20 points each.%r%t* Wild and Wild Draw Four cards are counted as 50 points each.%rThe winner receives 0 points.%r[repeat(-,78)]
&USTATS_ALL Game of Uno=$ustats/all:@pemit %#=[space(4)][ljust(All Uno Players,31)]Games[space(4)]Points[space(3)]Average%r[repeat(-,60)][parse(sortby(comp_records,parse(lattr(me/record_#*),moniker(after(##,RECORD_))/[v(##)]|)Player/[v(record_player)],|),%r[ifelse(u(filter_2games,##),rjust(setr(0,add(1,%q0)),2).%b,space(4))][ljust(first(##,/),25)][rjust(last(##,/),10)][rjust(extract(##,3,1,/),10)][rjust(round(extract(##,2,1,/),2),10)],|)]%r[repeat(-,60)]
&DO_SHUFFLE Game of Uno=@pemit/list v(watchers)=[moniker(*%0)] shuffles the uno deck.;&deck me=shuffle(v(deck) [iter(rest(v(stack)),switch(1,strmatch(##,?wild),wild,strmatch(##,?wdraw),wdraw,##))]);&stack me=first(v(stack))
&USHUFFLE Game of Uno=$ushuffle:@tr me/do_shuffle=%#
&DECK Game of Uno=b4 g2 r4 y7 yrev ydraw y6 y1 gskip bskip y2 r6 b7 y4 brev r2 brev r4 wild y4 b1 yrev wild g3 wild wdraw wild b3 bdraw yskip b5 b9 r5 b6 b1 b9 r3 y8 wdraw r2 r6 g7 r8 b6 g4 r8 b3 r9 y8 r9 g9 wdraw r3 r1 y3 grev bskip gdraw b4 y7 b2 b8 g2 bdraw y6 b5 g6 r0 y1 y3 r7 g1 g4 y0 ydraw g5 yskip y9 gskip y2 b0 r5 wdraw r7 y5 rskip g0 grev g8 rrev rskip y9 g8 g1 g7 rdraw r1 rdraw y5 gdraw b2 b8 g5 rrev g6 g3 b7
&RECORD_PLAYER Game of Uno=0/0/0
@set Game of Uno=INHERIT
@set Game of Uno=OPAQUE
@set Game of Uno=SAFE