@create OOC Room Parent=10
@NameFormat OOC Room Parent=[name(me)] - [u(area)] - [u(zone)][switch([isstaff(%#)],1,%b([num(me)][flags(me)]))]
@ExitFormat OOC Room Parent=Exits:%b%r[columns(ifelse(isstaff(%#),iter(lexits(me),strcat(ifelse(setr(1,orflags(##,D)),%ch%cr!%cn,),[first(name(##),;)]%b<[extract([fullname(##)],2,1,;)]>),%b,|),iter(%0,[first(name(##),;)]%b<[extract([fullname(##)],2,1,;)]>,%b,|)),26,|)]
@ConFormat OOC Room Parent=Contents:%r[columns(ifelse(isstaff(%#),iter(setdiff(lcon(me),%#),strcat(ifelse(orflags(##,D),%ch%cr!%cn,),ifelse(andflags(##,P!c),ansi(hr,-),),first(moniker(##),;)),%b,|),iter(setdiff(%0,%#),first(moniker(##),;),%b,|)),26,|)]
@Desc OOC Room Parent=[space(18)]Description to come soon.
@DescFormat OOC Room Parent=[repeat(-,78)]%r%r%t%0%r%r[repeat(-,78)]
@set OOC Room Parent=SAFE

@create Grid Room Parent=10
@Desc Grid Room Parent=[space(18)]Description to come soon.
@NameFormat Grid Room Parent=%r[center(<%b[ansi(h,[edit(name(me),and,&)] - [ansi(h,[u(area)])] - [ansi(h,[u(zone)])])][switch([isstaff(%#)],1,[ansi(hx,([num(me)][flags(me)]))])]%b>,78,-)]
@ExitFormat Grid Room Parent=Exits:%b%r[columns(ifelse(isstaff(%#),iter(lexits(me),strcat(ifelse(setr(1,orflags(##,D)),%ch%cr!%cn,),[first(edit(name(##),and,&),;)]%b<[ansi(h,[extract([fullname(##)],2,1,;)])]>),%b,|),iter(%0,[first(name(##),;)]%b<[ansi(h,[extract([fullname(##)],2,1,;)])]>,%b,|)),30,|)]
@ConFormat Grid Room Parent=Contents:%r[columns(ifelse(isstaff(%#),iter(setdiff(lcon(me),%#),strcat(ifelse(orflags(##,D),%ch%cr!%cn,),ifelse(andflags(##,P!c),ansi(hr,-),),first(moniker(##),;)),%b,|),iter(setdiff(%0,%#),first(moniker(##),;),%b,|)),26,|)]
@DescFormat Grid Room Parent=%r%t%0%r%r[repeat(-,78)]
&IC Grid Room Parent=1
@set Grid Room Parent=SAFE

@create CharGen Room Parent=10
@Desc CharGen Room Parent=[center(Description to come soon.,78)]%r%r[repeat(-,78)]
@NameFormat CharGen Room Parent=%r[name(me)] - [u(area)] - [u(zone)][switch([isstaff(%#)],1,%b([num(me)][flags(me)]))]%r[repeat(-,78)]%r
@ExitFormat CharGen Room Parent=%rExits:%r[columns(ifelse(isstaff(%#),iter(lexits(me),strcat(ifelse(setr(1,orflags(##,D)),%ch%cr!%cn,),[first(name(##),;)]%b<[extract([fullname(##)],2,1,;)]>),%b,|),iter(%0,[first(name(##),;)]%b<[extract([fullname(##)],2,1,;)]>,%b,|)),26,|)]
@set CharGen Room Parent=SAFE
@create Exit Parent=10
@Fail Exit Parent=You can not go that way since the room is [ansi(hr,locked)].
@Odrop Exit Parent=arrives from [ansi(ch,[name([home(me)])])].
@Osucc Exit Parent=heads into [ansi(hc,[name(num(loc(me)))])].
@Succ Exit Parent=You head into [ansi(hc,[name(num(loc(me)))])].
@set Exit Parent=INHERIT
@set Exit Parent=OPAQUE
@set Exit Parent=SAFE

@create RP Room Parent=10
@NameFormat RP Room Parent=[name(me)][switch([isstaff(%#)],1,%b([num(me)][flags(me)]))]%r[repeat(-,78)]
&C.LIST RP Room Parent=$+desc/list:@pemit %#=[center(<%b[ansi(h,Room Description List)]%b>,78,-)]%r%r[space(2)][edit([edit([setq(0,lcstr(sort(lattr(#59/desc_*))))][iter(%q0,[ljust(capstr_all(edit(edit(mid(##,strlen(desc_),strlen(##)),-,%b),_,)),24)][switch(mod(#@,3),0,%r%b)])],Of,of)],The,the)]%r%r[repeat(-,78)]
&C.LOCK RP Room Parent=$+lock:@lock [u(loc(%#)/exit)]=#1;@lock o=#1;&_lock [loc(%#)]=1;@pemit %#=[ansi(hc,Game:)] You've locked the doors.;@oemit %#=[ansi(hc,Game:)] [name(%#)] has locked the doors.
&C.UNLOCK RP Room Parent=$+unlock:@unlock [u(loc(%#)/exit)];@unlock o;&_lock [loc(%#)];@pemit %#=[ansi(hc,Game:)] You unlock the doors.;@oemit %#=[ansi(hc,Game:)] [name(%#)] has unlocked the doors.
&C.PREVIEW RP Room Parent=$+desc/preview *:@pemit %#=[center(<%b[ansi(h,Room Description: [capstr_all(%0)])]%b>,78,-)]%r%r[u(#59/desc_[edit(%0,%b,-)])]%r%r[repeat(-,78)]
&C.SET RP Room Parent=$+desc/set *:@switch [hasattr(#59,desc_[edit(%0,%b,-)])]=1,{&active [loc(%#)]=[edit(%0,%b,-)];@remit %#=[ansi(hc,Game:)] [name(%#)] has changed the room's description.},0,{@pemit %#=[ansi(hr,Error:)] That is not a valid selection. Check the list and try again.}
&C.RESET RP Room Parent=$+reset:&active [loc(%#)];@unlock [u(loc(%#)/exit)];@tel #60=[loc(%#)];@fo #60=@unlock out;@fo #60=home;&_lock [loc(%#)];@pemit %#=[ansi(hc,Game:)] You have reset the room. Doors are unlocked and description is reset.;@oemit %#=[ansi(hc,Game:)] [name(%#)] has reset the room.
&C.ADD RP Room Parent=$+desc/add *=*:@switch [hasattr(#59,desc_[edit(%0,%b,-)])]=0,{&desc_[edit(%0,%b,-)] #59=%1;@pemit %#=[ansi(hc,Game:)] You have added [ansi(h,capstr_all(%0))] to the description list with a description of:%r%r%t%1%r},1,{@pemit %#=[ansi(hr,Error:)] There is already a description by that name. Please choose another or ask the staff to remove it.}
@Desc RP Room Parent=[if(hasattr(loc(%#),active),[u(#59/desc_[u(loc(%#)/active)])],u(#59/desc_default))]
&C.COMMANDS RP Room Parent=$+commands:@pemit %#=[center(<%b[ansi(h,Command List)]%b>,78,-)]%r%r[space(2)][ljust(ansi(h,+desc/add <title>=<desc>)%b,28,.)]%bAdds a new desc to the description list.%r[space(2)][ljust(ansi(h,+desc/list)%b,28,.)]%bList the available descriptions for use.%r[space(2)][ljust(ansi(h,+desc/preview <title>)%b,28,.)]%bDisplays a preview of the specified description.%r[space(2)][ljust(ansi(h,+desc/set <title>)%b,28,.)]%bChanges the description of the room.%r[space(2)][ljust(ansi(h,+lock)%b,28,.)]%bLocks the room's doors and claims the room.%r[space(2)][ljust(ansi(h,+unlock)%b,28,.)]%bUnlocks the room's doors and frees up the room.%r[space(2)][ljust(ansi(h,+reset)%b,28,.)]%bResets the room to the default description, %r[space(31)]unlocks the doors and frees up the room.%r%r[repeat(-,78)]
&DESC_DEFAULT RP Room Parent=Welcome to [name(loc(%#))]. You are more than welcome to use this room to represent any rooms that you may need and are not created on the actual grid. We do ask that you use the [ansi(h,+lock)] command to lock the doors and claim the room as in use. This will prevent people from wondering into your scene and disrupting your RP. Feel free to add descriptions to the database if you do not find one that you like. You do not need to add [ansi(h,%%r%%t)] at the beginning of the new description and [ansi(h,%%r)] at the end. These are added automatically. For a list of commands that are available in this room, type '[ansi(h,+commands)]'.
@set RP Room Parent=INHERIT
@set RP Room Parent=SAFE
@parent RP Room Parent=#56

@create Parent Setup Bot=10
@set Parent Setup Bot=DARK
@set Parent Setup Bot=INHERIT
@set Parent Setup Bot=QUIET
@set Parent Setup Bot=ROBOT
@set Parent Setup Bot=SAFE
