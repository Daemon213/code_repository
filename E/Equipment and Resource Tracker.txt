#2959 - Equipment Database
#1094 - Equipment Manager BC

@create Equipment and Resource Tracker=10
@create Equipment Database=10

@VA Equipment and Resource Tracker=#2959
&C.EQUIPMENT/ADDCAT Equipment and Resource Tracker=$+equipment/addcat *:@switch [isstaff(%#)][not(ismember(get(%va/D.EQ-CATEGORIES), %0, |))]=0*,{@pemit %#=Permission denied.},10,{@pemit %#=There is already a category called [capstr(%0)].},11,{&D.EQ-CATEGORIES %va=[setunion(get(%va/D.EQ-CATEGORIES), lcstr(%0), |)];@pemit %#=Done. New equipment category created: "[titlestr(%0)]"}
&C.EQUIPMENT/DELCAT Equipment and Resource Tracker=$+equipment/delcat *:@switch [isstaff(%#)][ismember(get(%va/D.EQ-CATEGORIES), %0, |)][eq(attrcnt(%va/D.EQ-[edit(%0,%b,_)]-*), 0)]=0*,{@pemit %#=Permission denied.},10*,{@pemit %#=There's no category called [titlestr(%0)].},110,{@pemit %#=The category exists, but must be empty before you can delete it.},111,{&D.EQ-CATEGORIES %va=[setdiff(get(%va/D.EQ-CATEGORIES), lcstr(%0), |)];@pemit %#=Done. Category deleted: "[titlestr(%0)]"}
&C.EQUIPMENT/ADDINFO Equipment and Resource Tracker=$+equipment/addinfo *=*:@switch [isstaff(%#)][t(setr(0, partial(get(%va/D.EQ-CATEGORIES), %0, |)))][not(ismember(get(%va/D.INFO-[edit(%q0, %b, _)]), %1, |))]=0*,{@pemit %#=Permission denied.},10*,{@pemit %#=There's no category that matches that name.},110,{@pemit %#=There's already an info type in that category with that name.},111,{&D.INFO-[edit(%q0, %b, _)] %va=[setunion(get(%va/D.INFO-[edit(%q0, %b, _)]), lcstr(%1), |)];@pemit %#=New info entry added to [titlestr(%q0)]: [titlestr(%1)]}
&C.EQUIPMENT/DELINFO Equipment and Resource Tracker=$+equipment/delinfo *=*:@switch [isstaff(%#)][t(setr(0, partial(get(%va/D.EQ-CATEGORIES), %0, |)))][ismember(get(%va/D.INFO-[edit(%q0, %b, _)]), %1, |)]=0*,{@pemit %#=Permission denied.},10*,{@pemit %#=There's no category that matches that name.},110,{@pemit %#=There's no info type in that category with that name.},111,{&D.INFO-[edit(%q0, %b, _)] %va=[setdiff(get(%va/D.INFO-[edit(%q0, %b, _)]), lcstr(%1), |)];@pemit %#=Info entry deleted from [titlestr(%q0)]: [titlestr(%1)]}
&C.EQUIPMENT/ADD Equipment and Resource Tracker=$+equipment/add *=*:@switch [isstaff(%#)][t(setr(0, partial(get(%va/D.EQ-CATEGORIES), %0, |)))][valid(attrname, setr(1, D.EQ-[edit(%q0, %b, _)]-[edit(%1, %b, _)]))][not(hasattr(%va, %q1))]=0*,{@pemit %#=Permission denied.},10*,{@pemit %#=There's no category that matches that name.},110*,{@pemit %#=There's an invalid character in the item name you've given.},1110,{@pemit %#=There's already an item in that category with that name.},1111,{&%q1 %va=last edit:[time()]|edited by:%#;@pemit %#=New item created in [titlestr(%q0)]: [titlestr(%1)]}
&C.EQUIPMENT/SET Equipment and Resource Tracker=$+equipment/set *=*:@switch [setq(l, u(FN.FIND-ITEMS, first(%0, /[last(%0, /)])))][isstaff(%#)][t(%ql)][eq(words(%ql), 1)][ismember(get(%va/D.INFO-[setr(c, extract(%ql, 2, 1, -))]), last(%0, /), |)]=0*,{@pemit %#=Permission denied.},10*,{@pemit %#=There's no item that matches the text you entered.},110*,{@pemit %#=Your input returned too many results.},1110,{@pemit %#=That info type isn't valid for that item's category.},1111,{&%ql %va=update(update(update(get(%va/%ql), last(%0, /), %1), last edit, time()), edited by, %#);@pemit %#=Done. Item info updated.}
&C.EQUIPMENT/GIVE Equipment and Resource Tracker=$+equipment/give *=*:@switch [setq(l, u(FN.FIND-ITEMS, %1))][isstaff(%#)][isdbref(setr(0, pmatch+(%0)))][t(%ql)][eq(words(%ql), 1)]=0*,{@pemit %#=Permission denied.},10*,{@pemit %#=There is no player with that name.},110*,{@pemit %#=There's no item that matches the text you entered.},1110,{@pemit %#=Your input returned too many results.},1111,{&_EQ-[extract(%ql, 2, 1, -)]-[secs(local, 6)] %q0=attr:%ql|issued by:%#;@pemit %#=Done. Item given to [name(%q0)].;@pemit %q0=[alert(game)] %N has given you a piece of equipment: [titlestr(lcstr(edit(first(setr(1, rest(%ql, D.EQ-)), -), _, %b)))]/[titlestr(lcstr(edit(rest(%q1, -), _, %b)))].}
&FN.FIND-ITEMS Equipment and Resource Tracker=localize(ifelse(strmatch(%0, */*), ifelse(ismember(setr(0, lattr(%va/D.EQ-[edit(first(%0, /), %b, _)]*-[edit(rest(%0, /), %b, _)]*)), D.EQ-*-[edit(rest(%0, /), %b, _)]), grab(%q0, D.EQ-*-[edit(rest(%0, /), %b, _)]), %q0), ifelse(ismember(setr(0, lattr(%va/D.EQ-*-[edit(%0, %b, _)]*)), D.EQ-*-[edit(%0, %b, _)]), grab(%q0, D.EQ-*-[edit(%0, %b, _)]), %q0)))
&C.EQUIPMENT/LIST-NOARG Equipment and Resource Tracker=$+equipment/list:@pemit %#=[header(Equipment Categories)]%r[smartcol(iter(setr(0,get(%va/D.EQ-CATEGORIES)), titlestr(%i0), |, |), |, width(%#), space(3))]%r%rUse %xh'+equipment/list <category>'%xn to see each category.%rType %xh'+equipment/list all'%xn to see all equipment. Spammy!%r[footer([attrcnt(%va/D.EQ-*)] entries in [words(%q0, |)] categories)]
&C.EQUIPMENT/LIST-ARG Equipment and Resource Tracker=$+equipment/list *:@switch [t(setr(0, partial(setunion(get(%va/D.EQ-CATEGORIES), all, |), %0, |)))]=0,{@pemit %#=You must choose a valid category, or 'all'. See +equipment/list for a list of categories.},1,{@pemit %#=[header(Equipment List - [titlestr(%q0)])];@pemit %#=list(ifelse(strmatch(%q0, all), get(%va/D.EQ-CATEGORIES), %q0), [if(strmatch(%q0, all), [divider(titlestr(%i0))]%r)][smartcol(iter(sort(lattr(#2959/D.EQ-[edit(%i0, %b, _)]-*), i), titlestr(edit(rest(%i0, D.EQ-[ucstr(edit(%i1, %b, _))]-), _, %b)),, |), |, width(%#), space(1))], |);@pemit %#=footer([attrcnt(%va/D.EQ-[ifelse(strmatch(%q0, all), *, [ucstr(edit(%q0, %b, _))]-*)])] Entries)}
&C.EQUIPMENT-ARG Equipment and Resource Tracker=$+equipment *:@switch [gt(words(setr(0, lattr(%#/_EQ-*))), 0)][ifelse(isint(%0),ifelse(and(gt(%0, 0), lte(%0, words(%q0))),1[setq(l, access(get(%#/[setr(1, extract(%q0, %0, 1))]), attr))],0[setq(e, You have no equipment with that number.)]),[setq(l, u(FN.FIND-ITEMS, %0))][setq(1, filter(FIL.PLAYER-EQ, %q0,,, %#, %ql))][case(0,t(%ql),0[setq(e, The text you entered is not a valid equipment name.)],eq(words(%ql), 1),0[setq(e, Too many items match the text you entered. Lookups are done against the names of all equipment%, and then your character is checked to see if you have it.)],gt(words(%q1), 0),0[setq(e, You don't have any instances of that item.)],eq(words(%q1), 1),0[setq(e, You have more than one instance of that item. You'll need to view them by their item number. See +equipment for a list.)],1)])]=0*,{@pemit %#=You have no equipment notes set.},10,{@pemit %#=%qe},11,{@pemit %#=[header(Equipment Info: [titlestr(setr(n, lcstr(edit(rest(%ql, D.EQ-[setr(c,extract(%ql, 2, 1, -))]-), _, %b))))])];@pemit %#=[setq(d, get(%#/%q1))][udefault(FN.DISPLAY-%qc, %xh[ljust(Name, 15, .)]:%xn [titlestr(%qn)]%r%xh[ljust(Category, 15, .)]:%xn [titlestr(lcstr(edit(%qc, _, %b)))]%r[iter(setr(1, get(%va/%ql)), %xh[ljust(titlestr(first(%i0, :)), 15, .)]:%xn [wrap(rest(%i0, :), 61,,,, 17)], |, %r)], %ql)][if(t(access(%qd, details)), %r[divider(Details)]%rThe following are details for your specific piece of equipment:%r%r[access(%qd, details)])][if(ismember(%qd, *-transfer:#*, |), %r[divider(Transfer Log)]%r[itemize(iter(sort(graball(%qd, *-transfer:#*, |), n, |), [moniker(first(rest(%i0, :)))] > [moniker(rest(%i0))],|,|), |)])];@pemit %#=[footer()];}
&FIL.FIELD-SEARCH Equipment and Resource Tracker=strmatch(access(get(%va/%0), %1), *%2*)
&C.EQUIPMENT/SEARCH Equipment and Resource Tracker=$+equipment/search *=*:@switch [null(iter(lattr(%va/D.INFO-*), setq(i, setunion(%qi, get(%va/%i0), |))))][setq(i, setunion(%qi, name, |))][ismember(%qi, %0, |)][setq(r, ifelse(strmatch(%0, name), lattr(%va/D.EQ-*-*[edit(%1, %b, _)]*), filter(FIL.FIELD-SEARCH, lattr(%va/D.EQ-*),,, %0, %1)))][gt(words(%qr), 0)]=0*,{@pemit %#=That's not a valid info type. The following info fields are currently searchable:%r[itemize(%qi, |)]},10,{@pemit %#=Your search returned 0 results.},11,{@pemit %#=[header(Equipment Search Results)]%rThe following items matched your search:%r;@pemit %#=[smartcol(iter(sort(%qr, i), [titlestr(lcstr(edit(extract(%i0, 2, 1, -), _, %b)))]/[titlestr(lcstr(edit(rest(%i0, D.EQ-[extract(%i0, 2, 1, -)]-), _, %b)))],, |), |, width(%#), space(3))];@pemit %#=[footer([words(%qr)] results)]}
&C.EQUIPMENT/INFO Equipment and Resource Tracker=$+equipment/info *:@switch [setq(l, u(FN.FIND-ITEMS, %0))][t(%ql)][eq(words(%ql), 1)]=0*,{@pemit %#=No equipment matches that string.},10,{@pemit %#=More than one piece of equipment matches that string. Please be more specific.},11,{@pemit %#=[header(Equipment Info: [titlestr(setr(n, lcstr(edit(rest(%ql, D.EQ-[setr(c,extract(%ql, 2, 1, -))]-), _, %b))))])];@pemit %#=[udefault(FN.DISPLAY-%qc, %xh[ljust(Name, 15, .)]:%xn [titlestr(%qn)]%r%xh[ljust(Category, 15, .)]:%xn [titlestr(lcstr(edit(%qc, _, %b)))]%r[iter(setr(1, get(%va/%ql)), %xh[ljust(titlestr(first(%i0, :)), 15, .)]:%xn [wrap(rest(%i0, :), 61,,,, 17)], |, %r)], %ql)];@pemit %#=[footer()];}
&C.EQUIPMENT-NOARG Equipment and Resource Tracker=$+equipment:@switch [gt(words(lattr(%#/_EQ-*)), 0)]=0,{@pemit %#=You have no equipment notes set.},1,{@eval [iter(setr(e, lattr(%#/_EQ-*)), setq(l, setunion(%ql, rest(first(%i0, -[last(%i0, -)]), _EQ-), |)))][setq(0,strlen(words(%qe)))];@pemit %#=[header(Equipment List - %N)];@pemit %#=[list(%ql, [divider(titlestr(lcstr(edit(%i0, _, %b))))]%r[smartcol(iter(lattr(%#/_EQ-%i0-*), [ljust(member(%qe, %i0), %q0)]: [titlestr(lcstr(edit(rest(access(get(%#/%i0), attr), D.EQ-%i1-), _, %b)))],, |), |, width(%#), space(3))], |)];@pemit %#=[footer([attrcnt(%#/_EQ-*)] entries)]}
&FIL.PLAYER-EQ Equipment and Resource Tracker=strmatch(access(get(%1/%0), attr), %2)
&FN.DISPLAY-RANGED_WEAPONS Equipment and Resource Tracker=[setq(0, get(%va/%0))]Item readout for %xh[titlestr(lcstr(edit(%qc, _, %b)))]/[titlestr(%qn)]%xn:%r%r[smartcol(iter(strength size damage cost capacity range, %xh[rjust(titlestr(%i0), 10)]:%xn [access(%q0,%i0)][if(strmatch(%i0, cost), %b(Points: [case(access(%q0, cost), 1, 1, 2, 2, 3, 3, 4, 4, 5, 5)]))],, |), |,width(%#), space(2))]%r%rData from [access(%q0, cite)]. Last edited [elements(access(%q0, last edit), 2 3 5)] by [moniker(access(%q0, edited by))].
&C.EQUIPMENT/VIEW-NOARG Equipment and Resource Tracker=$+equipment/view *:@switch/first [not(strmatch(%0, *=*))][isdbref(setr(p, pmatch+(%0)))][gt(words(lattr(%qp/_EQ-*)), 0)]=0*, {},10*,{@pemit %#=No such player.},110,{@pemit %#=[name(%qp)] has no equipment notes set.},111,{@eval [iter(setr(e, lattr(%qp/_EQ-*)), setq(l, setunion(%ql, rest(first(%i0, -[last(%i0, -)]), _EQ-), |)))][setq(0,strlen(words(%qe)))];@pemit %#=[header(Equipment List - [name(%qp)])];@pemit %#=[list(%ql, [divider(titlestr(lcstr(edit(%i0, _, %b))))]%r[smartcol(iter(lattr(%qp/_EQ-%i0-*), [ljust(member(%qe, %i0), %q0)]: [titlestr(lcstr(edit(rest(access(get(%qp/%i0), attr), D.EQ-%i1-), _, %b)))],, |), |, width(%#), space(3))], |)];@pemit %#=[footer([attrcnt(%qp/_EQ-*)] entries)]}
&C.EQUIPMENT/VIEW-ARG Equipment and Resource Tracker=$+equipment/view *=*:@switch [isdbref(setr(p, pmatch+(%0)))][gt(words(setr(0, lattr(%qp/_EQ-*))), 0)][ifelse(isint(%1),ifelse(and(gt(%1, 0), lte(%1, words(%q0))),1[setq(l, access(setr(1, get(%qp/[extract(%q0, %1, 1)])), attr))],0[setq(e, You have no equipment with that number.)]),[setq(l, u(FN.FIND-ITEMS, %1))][setq(1, filter(FIL.PLAYER-EQ, %q0,,, %qp, %ql))][case(0,t(%ql),0[setq(e, The text you entered is not a valid equipment name.)],eq(words(%ql), 1),0[setq(e, Too many items match the text you entered. Lookups are done against the names of all equipment%, and then the character is checked to see if they have it.)],gt(words(%q1), 0),0[setq(e, [name(%qp)] doesn't have any instances of that item.)],eq(words(%q1), 1),0[setq(e, [name(%qp)] has more than one instance of that item. You'll need to view them by their item number. See +equipment/view [name(%qp)] for a list.)],1)])]=0*,{@pemit %#=No such player.},10*,{@pemit %#=[name(%qp)] has no equipment notes set.},110,{@pemit %#=%qe},111,{@pemit %#=[header(Equipment Info: [name(%qp)]'s [titlestr(setr(n, lcstr(edit(rest(%ql, D.EQ-[setr(c,extract(%ql, 2, 1, -))]-), _, %b))))])];@pemit %#=[udefault(FN.DISPLAY-%qc, %xh[ljust(Name, 15, .)]:%xn [titlestr(%qn)]%r%xh[ljust(Category, 15, .)]:%xn [titlestr(lcstr(edit(%qc, _, %b)))]%r[iter(setr(1, get(%va/%ql)), %xh[ljust(titlestr(first(%i0, :)), 15, .)]:%xn [wrap(rest(%i0, :), 61,,,, 17)], |, %r)], %ql)][if(t(access(%q1, details)), %r[divider(Details)]%rThe following are details for this specific piece of equipment:%r%r[access(%q1, details)])][if(ismember(%q1, *-transfer:#*, |), %r[divider(Transfer Log)]%r[itemize(iter(sort(graball(%q1, *-transfer:#*, |), n, |), [moniker(first(rest(%i0, :)))] > [moniker(rest(%i0))],|,|), |)])];@pemit %#=[footer()];}
&C.EQUIPMENT/TRANSFER Equipment and Resource Tracker=$+equipment/transfer *=*:@switch [gt(words(lattr(%#/_EQ-*)), 0)][isint(%0)][and(gt(%0, 0), lte(%0, words(setr(0, lattr(%#/_EQ-*)))))][and(isdbref(setr(p, pmatch+(%1))), or(isapproved(%qp), isstaff(%qp)))][setq(l, access(get(%#/[extract(%q0, %0, 1)]), attr))]=0*,{@pemit %#=You have no equipment notes set.},10*,{@pemit %#=You must specify equipment to transfer by number, to avoid potential partial matching mistakes.},110*,{@pemit %#=You have no equipment with that number. See +equipment for a list.},1110,{@pemit %#=You can only transfer equipment to staff or approved players.},1111,{@eval %#=[setq(n, titlestr(lcstr(edit(rest(%ql, D.EQ-[setr(c,extract(%ql, 2, 1, -))]-), _, %b))))][setq(c, titlestr(lcstr(edit(%qc, _, %b))))];&[setr(a, extract(%q0, %0, 1))] %#=[update(get(%#/%qa), [secs()]-transfer, %# %qp)];@cpattr %#/%qa=%qp/[replace(%qa, 3, secs(local, 6), -)];@wipe %#/%qa;@pemit %#=Done. You have transferred your %qc/%qn to [name(%qp)];@pemit %qp=%N has transferred [poss(%#)] %qc/%qn to you.;@cemit Monitor=[alert(eq transfer)] %N has transferred %qc/%qn to [name(%qp)].}
&C.EQUIPMENT/DESTROY Equipment and Resource Tracker=$+equipment/destroy */*:@switch [isstaff(%#)][isdbref(setr(p, pmatch+(%0)))][gt(words(setr(0, lattr(%qp/_EQ-*))), 0)][isint(%1)][and(gt(%1, 0), lte(%1, words(%q0)))][setq(l, access(get(%qp/[extract(%q0, %1, 1)]), attr))]=0*,{@pemit %#=Permission denied.},10*,{@pemit %#=No such player.},110*,{@pemit %#=[name(%qp)] has no equipment notes set.},1110*,{@pemit %#=You must specify equipment to destroy by number, to avoid potential partial matching mistakes.},11110,{@pemit %#=[name(%qp)] has no equipment with that number. See +equipment/view [name(%qp)] for a list.},11111,{@eval %#=[setq(n, titlestr(lcstr(edit(rest(%ql, D.EQ-[setr(c,extract(%ql, 2, 1, -))]-), _, %b))))][setq(c, titlestr(lcstr(edit(%qc, _, %b))))];@wipe %#/[extract(%q0, %1, 1)];@pemit %#=Done. You have destroyed [name(%qp)]'s %qc/%qn.;@pemit %qp=%N has destroyed your %qc/%qn.;@cemit Monitor=[alert(eq destroyed)] %N has destroyed [name(%qp)]'s %qc/%qn.}
&C.EQUIPMENT/REQUEST Equipment and Resource Tracker=$+equipment/request *:@switch [setq(l, u(FN.FIND-ITEMS, first(%0, =)))][setq(r, sub(get(%#/_RES-BANK), ladd(iter(lattr(%#/_RES-PENDING-*), access(get(%#/%i0), cost)))))][t(%ql)][eq(words(%ql), 1)][and(gte(%qr, case(access(setr(0, get(%va/%ql)), cost), 1, 1, 2, 2, 3, 3, 4, 4, 5, 5, 0)), gt(%qr, 0))]=0*,{@pemit %#=There's no item that matches the text you entered.},10*,{@pemit %#=Your input returned too many results.},110,{@pemit %#=You don't have enough resource points banked to afford that item. You only have %qr points available.},111,{@eval [setq(t, secs(local, 6))][setq(n, titlestr(lcstr(edit(rest(%ql, D.EQ-[setr(c,extract(%ql, 2, 1, -))]-), _, %b))))][setq(c, titlestr(lcstr(edit(%qc, _, %b))))][u(FN.RES-QUEUE-PUSH, %#, %qt)];&_RES-PENDING-%qt %#=item:%ql|cost:[case(access(%q0, cost), 1, 1, 2, 2, 3, 3, 4, 4, 5, 5, 0)];@trigger #15/TRIG_CREATE=%#, locate(#17, EQBUY, i), 1, EQ: %N, %r%r%N has requested the following piece of equipment: %qc/%qn. Approving or Completing this job will automatically note [obj(%#)] with the appropriate information%, and will deduct the appropriate amount from their Resource bank. Deleting or Denying this job will return the points for the pending spend back to their pool.[if(t(rest(%0, =)), %r%rThe following justification text was entered: [rest(%0,=)])],,,3;@pemit %#=Done. You have put in your equipment request. Note that the resource point cost of this request is counted against what you can buy until the request is processed.}
&FN.RES-QUEUE-PUSH Equipment and Resource Tracker=set(%0, _RES-QUEUE:[ifelse(t(get(%0/_RES-QUEUE)), %1%b[get(%0/_RES-QUEUE)], %1)])
&FN.RES-QUEUE-POP Equipment and Resource Tracker=localize([set(%0, _RES-QUEUE:[ldelete(setr(0, get(%0/_RES-QUEUE)), words(%q0))])]last(%q0))
&C.RESOURCES Equipment and Resource Tracker=$+resources:@pemit %#=[header(Resources - %N)]%rYou have [words(setr(p, lattr(%#/_RES-PENDING-*)))] pending Resource spend[ifelse(eq(words(%qp), 1),,s)][ifelse(t(%qp), :%r%r[smartcol(iter(%qp, [setq(l, get(%#/%i0))][setr(n, titlestr(lcstr(edit(rest(access(%ql, item), D.EQ-[setr(c,extract(access(%ql, item), 2, 1, -))]-), _, %b))))]/[setr(c, titlestr(lcstr(edit(%qc, _, %b))))] (Res [case(access(%ql, cost), 100, 5, 20, 4, 4, 3, 2, 2, 1, 1)], Cost [access(%ql, cost)][setq(t, add(%qt, access(%ql, cost)))]),, |), |, width(%#), space(2))], .)]%r%rYou have [setr(b, get(%#/_RES-BANK))] point[ifelse(eq(%qb, 1),, s)] in your Resource bank, of which you may use [sub(%qb, %qt)].%r[footer()]
&C.EQ-ARG Equipment and Resource Tracker=$+eq *:@fo %#=+equipment [escape(%0)]
&C.EQ Equipment and Resource Tracker=$+eq:@fo %#=+equipment
&C.EQ-SLASH Equipment and Resource Tracker=$+eq/*:@fo %#=+equipment/[escape(%0)]
&C.EQUIPMENT/DETAIL Equipment and Resource Tracker=$+equipment/detail */*=*:@switch [isstaff(%#)][isdbref(setr(p, pmatch+(%0)))][gt(words(setr(0, lattr(%qp/_EQ-*))), 0)][isint(%1)][and(gt(%1, 0), lte(%1, words(%q0)))][setq(l, access(get(%qp/[extract(%q0, %1, 1)]), attr))]=0*,{@pemit %#=Permission denied.},10*,{@pemit %#=No such player.},110*,{@pemit %#=[name(%qp)] has no equipment notes set.},1110*,{@pemit %#=You must specify equipment to detail by number, to avoid potential partial matching mistakes.},11110,{@pemit %#=[name(%qp)] has no equipment with that number. See +equipment/view [name(%qp)] for a list.},11111,{@eval %#=[setq(n, titlestr(lcstr(edit(rest(%ql, D.EQ-[setr(c,extract(%ql, 2, 1, -))]-), _, %b))))][setq(c, titlestr(lcstr(edit(%qc, _, %b))))];&[extract(%q0, %1, 1)] %qp=[update(get(%qp/[extract(%q0, %1, 1)]), details, %2)];@pemit %#=Done. You have added a detail to [name(%qp)]'s %qc/%qn (Item %1).;@pemit %qp=%N has added a detail to your %qc/%qn (Item %1).;}
&C.EQUIPMENT/SHOW Equipment and Resource Tracker=$+equipment/show *=*:@switch [gt(words(lattr(%#/_EQ-*)), 0)][isint(%0)][and(gt(%0, 0), lte(%0, words(setr(0, lattr(%#/_EQ-*)))))][isdbref(setr(p, pmatch+(%1)))][setq(l, access(setr(1, get(%#/[extract(%q0, %0, 1)])), attr))]=0*,{@pemit %#=You have no equipment notes set.},10*,{@pemit %#=You must specify equipment to show by number, to avoid potential partial matching mistakes.},110*,{@pemit %#=You have no equipment with that number. See +equipment for a list.},1110,{@pemit %#=No such player.},1111,{@eval [setq(n, lcstr(edit(rest(%ql, D.EQ-[setr(c,extract(%ql, 2, 1, -))]-), _, %b)))];@pemit %#=You show your [titlestr(lcstr(edit(%qc, _, %b)))]/[titlestr(%qn)] to [name(%qp)].;@pemit %qp=[header(Equipment Info: %N's [titlestr(%qn)])];@pemit %qp=[udefault(FN.DISPLAY-%qc, %xh[ljust(Name, 15, .)]:%xn [titlestr(%qn)]%r%xh[ljust(Category, 15, .)]:%xn [titlestr(lcstr(edit(%qc, _, %b)))]%r[iter(setr(1, get(%va/%ql)), %xh[ljust(titlestr(first(%i0, :)), 15, .)]:%xn [wrap(rest(%i0, :), 61,,,, 17)], |, %r)], %ql)][if(t(access(%q1, details)), %r[divider(Details)]%rThe following are details for your specific piece of equipment:%r%r[access(%q1, details)])];@pemit %qp=[footer()];@pemit %qp=%N shows you [poss(%#)] [titlestr(lcstr(edit(%qc, _, %b)))]/[titlestr(%qn)].}
&FN.DISPLAY-ARMOR Equipment and Resource Tracker=[setq(0, get(%va/%0))]Item readout for %xh[titlestr(lcstr(edit(%qc, _, %b)))]/[titlestr(%qn)]%xn:%r%r[smartcol(iter(rating defense speed strength cost, %xh[rjust(titlestr(%i0), 10)]:%xn [access(%q0, %i0)][if(strmatch(%i0, cost), %b(Points: [case(access(%q0, cost), 1, 1, 2, 2, 3, 3, 4, 4, 5, 5)]))],, |), |, width(%#), space(2))]%r%rData from [access(%q0, cite)]. Last edited [elements(access(%q0, last edit), 2 3 5)] by [moniker(access(%q0, edited by))].
&FN.DISPLAY-MELEE_WEAPONS Equipment and Resource Tracker=[setq(0, get(%va/%0))]Item readout for %xh[titlestr(lcstr(edit(%qc, _, %b)))]/[titlestr(%qn)]%xn:%r%r[smartcol(iter(damage size durability strength cost, %xh[rjust(titlestr(%i0), 10)]:%xn [access(%q0, %i0)][if(strmatch(%i0, cost), %b(Points: [case(access(%q0, cost), 1, 1, 2, 2, 3, 3, 4, 4, 5, 5)]))],, |), |, width(%#),space(2))]%r%r%xhNotes:%xn [wrap(ifelse(access(%q0, notes), access(%q0, notes), None.), 71,,,, 7)]%r%rData from [access(%q0, cite)]. Last edited [elements(access(%q0, last edit), 2 3 5)] by [moniker(access(%q0, edited by))].
&FN.DISPLAY-AMMUNITION Equipment and Resource Tracker=[setq(0, get(%va/%0))]Item readout for %xh[titlestr(lcstr(edit(%qc, _, %b)))]/[titlestr(%qn)]%xn:%r%r[iter(cost effect notes, %xh[rjust(titlestr(%i0), 6)]:%xn [wrap([access(%q0, %i0)][if(strmatch(%i0, cost),%b(Points: [case(access(%q0, cost), 1, 1, 2, 2, 3, 3, 4, 4, 5, 5)]))], 70,,,, 8)],, %r)]%r%rData from [access(%q0,cite)]. Last edited [elements(access(%q0, last edit), 2 3 5)] by [moniker(access(%q0, edited by))].
&FN.DISPLAY-OTHER_EQUIPMENT Equipment and Resource Tracker=[setq(0, get(%va/%0))]Item readout for %xh[titlestr(lcstr(edit(%qc, _, %b)))]/[titlestr(%qn)]%xn:%r%r[smartcol(iter(cost size structure durability, %xh[rjust(titlestr(%i0), 10)]:%xn [access(%q0, %i0)][if(strmatch(%i0, cost), %b(Points: [case(access(%q0, cost), 1, 1, 2, 2, 3, 3, 4, 4, 5, 5)]))],, |), |, width(%#),space(2))]%r%r%xhNotes:%xn [wrap(if(access(%q0, notes), access(%q0, notes), None.), 71,,,, 7)]%r%rData from [access(%q0, cite)]. Last edited [elements(access(%q0, last edit), 2 3 5)] by [moniker(access(%q0, edited by))].
&FN.DISPLAY-SHIELDS Equipment and Resource Tracker=[setq(0, get(%va/%0))]Item readout for %xh[titlestr(lcstr(edit(%qc, _, %b)))]/[titlestr(%qn)]%xn:%r%r[smartcol(iter(cost defense strength, %xh[rjust(titlestr(%i0), 10)]:%xn [access(%q0, %i0)][if(strmatch(%i0, cost),%b(Points: [case(access(%q0, cost), 1, 1, 2, 2, 3, 3, 4, 4, 5, 5)]))],, |), |, width(%#), space(2))]%r%r%xhNotes:%xn [wrap(ifelse(access(%q0, notes), access(%q0, notes), None.), 71,,,, 7)]%r%rData from [access(%q0, cite)]. Last edited [elements(access(%q0, last edit), 2 3 5)] by [moniker(access(%q0, edited by))].
&C.RESOURCES-ARG Equipment and Resource Tracker=$+resources *:@switch [setq(0, pmatch+(%0))][isdbref(%q0)][t(isapproved(%q0))]=0*,{@pemit %#=There is no player by that name.},10,{@pemit %#=That player is not approved for play.},11,{@pemit %#=[header(Resources - [name(%q0)])]%r[capstr(subj(%q0))] has [words(setr(p, lattr(%q0/_RES-PENDING-*)))] pending Resource spend[ifelse(eq(words(%qp), 1),,s)][ifelse(t(%qp), :%r%r[smartcol(iter(%qp, [setq(l, get(%q0/%i0))][setr(n, titlestr(lcstr(edit(rest(access(%ql, item), D.EQ-[setr(c,extract(access(%ql, item), 2, 1, -))]-), _, %b))))]/[setr(c, titlestr(lcstr(edit(%qc, _, %b))))] (Res [case(access(%ql, cost), 100, 5, 20, 4, 4, 3, 2, 2, 1, 1)], Cost [access(%ql, cost)][setq(t, add(%qt, access(%ql, cost)))]),, |), |, width(%#), space(2))], .)]%r%r[capstr(subj(%q0))] has [setr(b, get(%q0/_RES-BANK))] point[ifelse(eq(%qb, 1),, s)] in [poss(%q0)] Resource bank, of which [subj(%q0)] may use [sub(%qb, %qt)].%r[footer()]}
&C.RESOURCES/GIVE Equipment and Resource Tracker=$+resources/give *=*:@switch [setq(0, pmatch+(%0))][isstaff(%#)][isdbref(%q0)][eq(isapproved(%q0), 1)][isint(%1)]=0*,{@pemit %#=Permission denied.},10*,{@pemit %#=There is no player by that name.},110*,{@pemit %#=That player is not approved for play.},1110,{@pemit %#=You must enter a valid number to give.},1111,{&_RES-BANK %q0=[add(get(%q0/_RES-BANK), %1)][logtext(add, resources, 3|%#|%q0|%1)];@pemit %#=Done. You have changed [name(%q0)]'s resource bank by %1.;@pemit %q0=[alert(resources)] %N has changed your resource bank by %1.}
&C.RESOURCES/PAY Equipment and Resource Tracker=$+resources/pay *=*:@switch [setq(0, pmatch+(%0))][isdbref(%q0)][eq(isapproved(%q0), 1)][and(strmatch(loc(%q0), %l), cansee(%#, %q0))][and(gt(%1, 0), regmatch(%1,^\[0-9\]*$))][setq(r, sub(get(%#/_RES-BANK), ladd(iter(lattr(%#/_RES-PENDING-*), access(get(%#/%i0), cost)))))][gte(%qr, %1)]=0*,{@pemit %#=There is no player by that name.},10*,{@pemit %#=That player is not approved for play.},110*,{@pemit %#=The recipient must be in your location and visible to you.},1110*,{@pemit %#=You must enter a positive integer for the amount to give.},11110,{@pemit %#=You don't have enough resource points to give %1 to [name(%q0)].},11111,{&_RES-BANK %q0=[add(get(%q0/_RES-BANK), %1)][logtext(add, resources, 4|%#|%q0|%1)];&_RES-BANK %#=[sub(get(%#/_RES-BANK), %1)];@pemit %#=Done. You have given %1 resources points to [name(%q0)].;@pemit %q0=[alert(resources)] %N has give you %1 resource points.}
&C.RESOURCES/LOG Equipment and Resource Tracker=$+resources/log:@switch [setq(0, logtext(search, resources, |%#|))][setq(1, rest(logtext(list, resources)))][not(eq(words(%q0, ::), 0))]=0,{@pemit %#=No resource records for your character. Please note that logging started on March 18th, 2013.},1,{@pemit %#=[header(Resources Log)]%r[iter(revwords(extract(revwords(%q0, ::), 1, 10, ::, ::), ::), [setq(s, first(%i0, |))]%b [timefmt($d $b '$g, %qs)]: [case(extract(%i0, 2, 1, |), 1,[moniker(extract(%i0, 3, 1, |))] approved an eq request for a cost of [extract(%i0, 5, 1, |)],2,Monthly resource allotment of [extract(%i0, 5, 1, |)] received.,3,[moniker(extract(%i0, 3, 1, |))] awarded [extract(%i0, 5, 1, |)] resource point[ifelse(eq(extract(%i0, 5, 1, |), 1), ., s.)],4,[moniker(extract(%i0, 3, 1, |))] paid [extract(%i0, 5, 1, |)] resource point[ifelse(eq(extract(%i0, 4, 1, |), 1), ., s.)])], ::, %r)]%r[footer([words(%q0, ::)] entries)]}
&C.RESOURCES/LOG-ARG Equipment and Resource Tracker=$+resources/log *:@switch [isstaff(%#)][setq(p, pmatch+(%0))][isdbref(%qp)][setq(0, logtext(search, resources, |%qp|))][setq(1, rest(logtext(list, resources)))][not(eq(words(%q0, ::), 0))]=0*,{@pemit %#=Permission denied.},10*,{@pemit %#=No such player.},110,{@pemit %#=No resource records for [name(%qp)]. Please note that logging started on March 18th, 2013.},111,{@pemit %#=[header(Resources Log - [name(%qp)])]%r[iter(revwords(extract(revwords(%q0, ::), 1, 10, ::, ::), ::), [setq(s, first(%i0, |))]%b [timefmt($d $b '$g, %qs)]: [case(extract(%i0, 2, 1, |), 1,[moniker(extract(%i0, 3, 1, |))] approved an eq request for a cost of [extract(%i0, 5, 1, |)],2,Monthly resource allotment of [extract(%i0, 5, 1, |)] received.,3,[moniker(extract(%i0, 3, 1, |))] awarded [extract(%i0, 5, 1, |)] resource point[ifelse(eq(extract(%i0, 5, 1, |), 1), ., s.)],4,[moniker(extract(%i0, 3, 1, |))] paid [extract(%i0, 5, 1, |)] resource point[ifelse(eq(extract(%i0, 5, 1, |), 1), ., s.)])], ::, %r)]%r[footer([words(%q0, ::)] entries)]}
&C.EQUIPMENT/SELL Equipment and Resource Tracker=$+equipment/sell *=*=*:@switch [setq(l, u(FN.FIND-ITEMS, %1))][isstaff(%#)][isdbref(setr(0, pmatch+(%0)))][t(%ql)][eq(words(%ql), 1)][and(gte(%2, 0), regmatch(%2,^\[0-9\]*$))][setq(r, sub(get(%q0/_RES-BANK), ladd(iter(lattr(%q0/_RES-PENDING-*), access(get(%q0/%i0), cost)))))][gte(%qr, %2)]=0*,{@pemit %#=Permission denied.},10*,{@pemit %#=There is no player with that name.},110*,{@pemit %#=There's no item that matches the text you entered.},1110*,{@pemit %#=Your input returned too many results.},11110*,{@pemit %#=You must enter a number 0 or greater for the cost.},111110,{@pemit %#=[name(%q0)] doesn't have enough resource points for that transaction.},111111,{&_EQ-[extract(%ql, 2, 1, -)]-[secs(local, 6)] %q0=attr:%ql|issued by:%#;&_RES-BANK %q0=[sub(get(%q0/_RES-BANK), %2)];@pemit %#=Done. Item sold to [name(%q0)] for a cost of %2.[logtext(add, resources, 1|%#|%q0|%2)];@pemit %q0=[alert(game)] %N has sold you a piece of equipment at a cost of %2: [titlestr(lcstr(edit(first(setr(1, rest(%ql, D.EQ-)), -), _, %b)))]/[titlestr(lcstr(edit(rest(%q1, -), _, %b)))].}
&FN.DISPLAY-MENTAL_EQUIPMENT Equipment and Resource Tracker=[setq(0, get(%va/%0))]Item readout for %xh[titlestr(lcstr(edit(%qc, _, %b)))]/[titlestr(%qn)]%xn:%r%r[smartcol(iter(cost|die bonus|durability|size|structure, %xh[rjust(titlestr(%i0), 10)]:%xn[access(%q0,%i0)][if(strmatch(%i0, cost), %b(Points: [case(access(%q0, cost), 1, 1, 2, 2, 3, 3, 4, 4, 5, 5)]))], |,|), |,width(%#), space(2))]%r%r%xhEffect:%xn [access(%q0, effect)]%r%rData from [access(%q0, cite)]. Last edited [elements(access(%q0, last edit), 2 3 5)] by [moniker(access(%q0, edited by))].
&FN.DISPLAY-PHYSICAL_EQUIPMENT Equipment and Resource Tracker=[setq(0, get(%va/%0))]Item readout for %xh[titlestr(lcstr(edit(%qc, _, %b)))]/[titlestr(%qn)]%xn:%r%r[smartcol(iter(cost|die bonus|durability|size|structure, %xh[rjust(titlestr(%i0), 10)]:%xn[access(%q0,%i0)][if(strmatch(%i0, cost), %b(Points: [case(access(%q0, cost), 1, 1, 2, 2, 3, 3, 4, 4, 5, 5)]))], |,|), |,width(%#), space(2))]%r%r%xhEffect:%xn [access(%q0, effect)]%r%rData from [access(%q0, cite)]. Last edited [elements(access(%q0, last edit), 2 3 5)] by [moniker(access(%q0, edited by))].
&FN.DISPLAY-SOCIAL_EQUIPMENT Equipment and Resource Tracker=[setq(0, get(%va/%0))]Item readout for %xh[titlestr(lcstr(edit(%qc, _, %b)))]/[titlestr(%qn)]%xn:%r%r[smartcol(iter(cost|die bonus|durability|size|structure, %xh[rjust(titlestr(%i0), 10)]:%xn[access(%q0,%i0)][if(strmatch(%i0, cost), %b(Points: [case(access(%q0, cost), 1, 1, 2, 2, 3, 3, 4, 4, 5, 5)]))], |,|), |,width(%#), space(2))]%r%r%xhEffect:%xn [access(%q0, effect)]%r%rData from [access(%q0, cite)]. Last edited [elements(access(%q0, last edit), 2 3 5)] by [moniker(access(%q0, edited by))].
&FN.DISPLAY-SUPERNATURAL_EQUIPMENT Equipment and Resource Tracker=[setq(0, get(%va/%0))]Item readout for %xh[titlestr(lcstr(edit(%qc, _, %b)))]/[titlestr(%qn)]%xn:%r%r[smartcol(iter(cost|die bonus|durability|size|structure, %xh[rjust(titlestr(%i0), 10)]:%xn[access(%q0,%i0)][if(strmatch(%i0, cost), %b(Points: [case(access(%q0, cost), 1, 1, 2, 2, 3, 3, 4, 4, 5, 5)]))], |,|), |,width(%#), space(2))]%r%r%xhEffect:%xn [access(%q0, effect)]%r%rData from [access(%q0, cite)]. Last edited [elements(access(%q0, last edit), 2 3 5)] by [moniker(access(%q0, edited by))].
&FN.DISPLAY-VEHICLES Equipment and Resource Tracker=[setq(0, get(%va/%0))]Item readout for %xh[titlestr(lcstr(edit(%qc, _, %b)))]/[titlestr(%qn)]%xn:%r%r[smartcol(iter(handling size dur/struc speed cost, %xh[rjust(titlestr(%i0), 10)]:%xn [access(%q0, %i0)][if(strmatch(%i0, cost), %b(Points: [case(access(%q0, cost), 1, 1, 2, 2, 3, 3, 4, 4, 5, 5)]))],, |), |, width(%#),space(2))]%r%r%xhNotes:%xn [wrap(ifelse(access(%q0, notes), access(%q0, notes), None.), 71,,,, 7)]%r%rData from [access(%q0, cite)]. Last edited [elements(access(%q0, last edit), 2 3 5)] by [moniker(access(%q0, edited by))].
@set Equipment and Resource Tracker=INHERIT
@set Equipment and Resource Tracker=SAFE
@rxlevel Equipment and Resource Tracker=
@txlevel Equipment and Resource Tracker=

&D.EQ-CATEGORIES Equipment Database=ammunition|armor|melee weapons|mental equipment|other equipment|physical equipment|ranged weapons|shields|social equipment|supernatural equipment|test items|vehicles
&D.INFO-TEST_ITEMS Equipment Database=bar|testing
&D.INFO-ARMOR Equipment Database=cite|cost|defense|rating|speed|strength
&D.INFO-MELEE_WEAPONS Equipment Database=cite|cost|damage|durability|notes|size|strength
&D.INFO-RANGED_WEAPONS Equipment Database=capacity|cite|cost|damage|notes|range|size|strength|type
&D.INFO-AMMUNITION Equipment Database=cite|cost|effect|notes
&D.INFO-VEHICLES Equipment Database=acceleration|cite|cost|durability|handling|max speed|occupants|safe speed|size|structure|type
&D.INFO-OTHER_EQUIPMENT Equipment Database=cite|cost|durability|notes|size|structure
&D.INFO-MENTAL_EQUIPMENT Equipment Database=cost|die bonus|durability|effect|size|structure
&D.INFO-PHYSICAL_EQUIPMENT Equipment Database=cost|die bonus|durability|effect|size|structure
&D.INFO-SOCIAL_EQUIPMENT Equipment Database=cost|die bonus|durability|effect|size|structure
&D.INFO-SUPERNATURAL_EQUIPMENT Equipment Database=cost|die bonus|durability|effect|size|structure
&D.EQ-TEST_ITEMS-TESTAROO Equipment Database=bar:d3|edited by:#1094|last edit:Sun Feb 16 02:26:41 2014|testing:d3
@rxlevel Equipment Database=
@txlevel Equipment Database=
