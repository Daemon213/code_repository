@@ This amusing bit of code is a simple vendor based on girls and boys found at Renn Faires. 
@@ I combined several jobs into one, and then used SCA terms to create a 'SCA-tender' to help 
@@ give atmosphere to a MUSH sca event. The most interesting bit of code (I think) is the origin 
@@ of my vendor code for switching between different people names based on the time of day.

@create Wandering Wench=10
@Desc Wandering Wench=%r[u(wench-name)] is a pretty teenaged girl dressed in a white chemise and a low-cut boned bodice that causes her large breasts to defy gravity in the most amazing ways. She has pulled one side of her skirt up and tucked it in at the waist, displaying her finely-curved legs and the dusty tops of her bare feet.%r%rThis poor girl is obviously over-worked. She has a basket over one arm heaped with cut flowers and ripe peaches, she's pushing a wheeled barrel with the word "pickles" carved into it, and there's a "T"-shaped pole attached to the barrel from which dried flower garlands hang. It's all quite a load for her to move around with.%r%rYou can 'buy' one of several products from [u(wench-name)]: 'flower','pickle','peach', or 'garland' ('buy <product>').%rYou can also ask her to 'serve' you something from the food court: 'turkey leg','stir fry'(veggie), or 'steak on a stick' ('serve <food>')%r
@VA Wandering Wench=^* has arrived.:@trigger me/who-works;@switch u(%#/sex)=f*,{@wait 2=@trig me/femit-trig=%N},m*,{@wait 2=@trig me/memit-trig=%N}
&MEMIT-TRIG Wandering Wench=@remit here= [switch(rand(10),0,[u(wench-name)] the Wandering Wench approaches %0 and says "Would M'lord like to buy a pretty flower for someone special?".,1,[u(wench-name)] the Wandering Wench approaches %0 and licks her lips suggestively before saying "Would M'lord like to buy a tasty pickle?".,2,[u(wench-name)] the Wandering Wench approaches %0 and says "Would M'lord like to buy a garland for a pretty lady?".,3,[u(wench-name)] the Wandering Wench elbows her way through the crowd yelling "Make way...make way for [switch(rand(4),0,Lord David Silverbow,1, Sir Gunther von Falkenchild,2, Master of the Laurel Jean-Luc d'Lys,3, Master of the Pelican Sir Eberwolf the Saxon)]",4,[u(wench-name)] the Wandering Wench approaches %0 and inhales deeply. A foil-wrapped chocolate coin surfaces between her uplifted breasts. "Sweets for the sweet...M'lord!",5,[u(wench-name)] the Wandering Wench works her way through the crowd calling "FLOW-ers! PICK-els! PEACH-es! GAR-lands for SALE...".,6, [u(wench-name)] the Wandering Wench approaches %0 and says "Would M'lord like a kiss from a Wandering Wench?",7,[u(wench-name)] the Wandering Wench works her way through the crowd to %0 and angles her considerable cleavage into his direct line of vision. "I have fresh ripe juicy peaches for sale...M'lord".,8,[u(wench-name)] the Wandering Wench stomps through the crowd yelling "Make way...make way for [switch(rand(4),0, young Lucy the Lost,1,Serafina the Dancer,2,Lady Donna de Cerdana,3,Mistress of the Laurel Diana of Hatfield)]",9,[u(wench-name)] the Wandering Wench stands to one side giggling with a group of friends.)]
&FEMIT-TRIG Wandering Wench=@remit here= [switch(rand(10),0,[u(wench-name)] the Wandering Wench approaches %0 and says "Would M'lady like to buy a pretty flower?".,1,[u(wench-name)] the Wandering Wench approaches %0 and says "Would M'lady like to buy a tasty pickle?".,2,[u(wench-name)] the Wandering Wench approaches %0 and says "Would M'lady like to buy a garland for her hair?".,3,[u(wench-name)] the Wandering Wench barrels through the crowd yelling "Make way...make way for Sir [switch(rand(4),0,Laird Dunstan,1,Richard of the Red Lion,2,James of Fenwyck Castle,3,Olaf Redbeard)]",4, [u(wench-name)] the Wandering Wench approaches %0 and pulls a foil-wrapped chocolate coin from behind her ear with a flourish. "You almost missed this...M'lady".,5,[u(wench-name)] the Wandering Wench works her way through the crowd calling "FLOW-ers! PICK-els! PEACH-es! GAR-lands for SALE...".,6, [u(wench-name)] the Wandering Wench approaches %0 and says "Would M'lady like to buy a garland for her hair?".,7,[u(wench-name)] the Wandering Wench works her way through the crowd trilling "PEA-ches for sale...fresh ripe juicy PEA-ches...".,8,[u(wench-name)] the Wandering Wench pushes through the crowd yelling "Make way...make way for [switch(rand(4),0, Sir Elizabeth Pembroke,1,Mistress of the Laurel Gunnora Eriksdotter,2,Lady Donna de Cerdana,3,Mistress of the Pelican Aoefa Godwin)]",9,[u(wench-name)] the Wandering Wench stands to one side giggling with a friend.)]
&WHO-WORKS Wandering Wench=@switch [gte(mid(vtime(),11,2),6)]+[gte(mid(vtime(),11,2),12)]+[gte(mid(vtime(),11,2), 18)]=0+0+0,{@switch v(timez)=night,,{@emit Molly comes by and takes over for Shannon as the Wandering Wench.; &wench-name me=Molly; &timez me=night;}},1+0+0,{@switch v(timez)=morning,,{@emit Jenny comes by and takes over for Molly as the Wandering Wench.; &wench-name me=Jenny; &timez me=morning;}},1+1+0,{@switch v(timez)=afternoon,,{@emit Lizzie comes by and takes over for Jenny as the Wandering Wench.; &wench-name me=Lizzie; &timez me=afternoon;}},{@switch v(timez)=night,,{@emit Shannon comes by and takes over for Lizzie as the Wandering Wench.; &wench-name me=Shannon; &timez me=night;}}
@VB Wandering Wench=$reset:@trigger me/startup; @emit The [name(me)] flops down for a minute to rest, then jumps up, ready to a'wander once again.
@Startup Wandering Wench=@halt me
@VC Wandering Wench=$buy *:@switch member(u(PRODUCTS),%0)=0,{@pemit %#=That is not something [u(WENCH-NAME)] the Wandering Wench sells. Try to 'buy' a 'flower','pickle','peach', or 'garland' ('buy <product>').},{@switch %0=flower,{@pemit %#= You buy a flower from [u(WENCH-NAME)] the Wandering Wench. It will either be a red rose, a white carnation, or a yellow daffodil. It has a little green tube on the cut end filled with water to keep the bloom fresh.; @oemit %# = %N looks through [u(WENCH-NAME)] the Wandering Wench's basket, and chooses a flower to buy.},pickle,{@pemit %# = You buy a pickle from [u(WENCH-NAME)] the Wandering Wench. She opens the pickle barrel and chooses a big one, just for you. The brine in the barrel is filled with floating herbs and peeled cloves of garlic. The pickle smells delicious.;@oemit %# = %N buys a pickle from [u(WENCH-NAME)] the Wandering Wench.},peach,{@pemit %# = You buy peach from [u(WENCH-NAME)] the Wandering Wench. The peach is warm and fragrant, and almost bursting with ripeness.; @oemit %# = %N looks through [u(WENCH-NAME)] the Wandering Wench's basket, and chooses a peach to buy.},garland,{@pemit %# = You buy a garland of dried flowers from [u(WENCH-NAME)] the Wandering Wench. Each garland features two complimentary colors of dried flowers with matching ribbons hanging down. It is meant to be worn as a circlet on a woman's head.; @oemit %# = %N looks over [u(WENCH-NAME)] the Wandering Wench's garland pole, and chooses a garland of dried flowers to buy.},{@pemit %#=[u(WENCH-NAME)] the Wandering Wench seems to be malfunctioning. Please 'reset' her, and if you still cannot buy your product, please +mail [name(owner(me))] about the problem!}}
&PRODUCTS Wandering Wench=flower pickle peach garland
&TIMEZ Wandering Wench=afternoon
@VE Wandering Wench=$wench *:@switch [or(member(v(FRIENDS),%#),not(orflags(%#,WZwi))]=0,,{@fo me = %0}
&FRIENDS Wandering Wench=#0
&WENCH-NAME Wandering Wench=Lizzie
&SERVE-CMD Wandering Wench=$serve *:@switch member(u(FOODS),%0,|)=0,{@pemit %#=That is not a food item [u(WENCH-NAME)] the Wandering Wench can get you. Try requsting she 'serve' you a 'turkey leg', 'stir fry', or 'steak on a stick'. ('serve <food>').},{@switch %0=stir fry,{@pemit %# = You ask [u(WENCH-NAME)] the Wandering Wench to serve you some stir fry from the food court area. She scampers back with a heaping plate of white rice and stir-fried vegetables seasoned with ginger and soy sauce. She hands you a plastic fork and a pair of wooden chop sticks, but no napkin.; @oemit %# = %N asks [u(WENCH-NAME)] the Wandering Wench to serve some stir fried vegetables from the food court area. She scampers back with it a few moments later.},turkey leg,{@pemit %# = You ask [u(WENCH-NAME)] the Wandering Wench to serve you a turkey leg from the food court area. She scampers back with a large roasted turkey drumstick with a napkin wrapped around the base. The skin has been crisped to a medium brown color, and the meat is cooked through but still juicy.; @oemit %# = %N asks [u(WENCH-NAME)] the Wandering Wench to serve a turkey leg from the food court area. She scampers back with it a few moments later.},steak on a stick,{@pemit %# = You ask [u(WENCH-NAME)] the Wandering Wench to serve you some "steak on a stick" from the food court area. She scampers back with a long wooden skewer threaded through with strips of grilled marinated beef. There seems to be no napkin with it.; @oemit %# = %N asks [u(WENCH-NAME)] the Wandering Wench to serve some "steak on a stick" from the food court area. She scampers back with it a few moments later.},{@pemit %#=[u(WENCH-NAME)] the Wandering Wench seems to be malfunctioning. Please 'reset' her, and if you still cannot buy your product, please +mail [name(owner(me))] about the problem!}}
&FOODS Wandering Wench=turkey leg|stir fry|steak on a stick|
&CREDITS Wandering Wench=This SCA-tender written/desced/coded by Gwen Morse (http://www.gwen-morse.com/wiki/index.php/MUSHcode)
@set Wandering Wench=MONITOR
